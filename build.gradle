apply plugin: 'build-dashboard'

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    
    group = "org.tweetwallfx"
    
    repositories {
        jcenter()
    }
    dependencies {
        //        compile fileTree(dir: 'libs', include: '*.jar')
        testCompile 'junit:junit:4.11'
    }
    version = '1.0'
    
    jar {
        manifest.attributes provider: 'gradle'
    }

    compileJava {
        options.compilerArgs << "-Xlint:all"// << "-Xlint:deprecation"
    } 

    findbugs {
        ignoreFailures = false
    }
    
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
    
    tasks.withType(JavaExec) {
        args "-oAuthFile", "MyOAuth.properties"
    }
}

project (":TweetWallFX-CommandlineArguments") {
    dependencies {
        compile group: 'org.netbeans.api', name: 'org-openide-util-lookup', version: 'RELEASE80'
        compile group: "com.beust", name: "jcommander", version: "1.48"
    }
}

project(":TweetWallFX-Tweet-API") {
    dependencies {
        compile group: 'org.netbeans.api', name: 'org-openide-util-lookup', version: 'RELEASE80'
    }
}

project(":TweetWallFX-Tweet-Impl-Twitter4J") {
    dependencies {
        compile group: "org.twitter4j", name: "twitter4j-core", version: "4.0.3"
        compile group: "org.twitter4j", name: "twitter4j-stream", version: "4.0.3"
        compile group: "com.beust", name: "jcommander", version: "1.48"
        compile group: "log4j", name: "log4j", version: "1.2.17"
        compile project(':TweetWallFX-Tweet-API')
        compile project(':TweetWallFX-CommandlineArguments')
    }
}

project(":TweetWallFX-Core"){
    dependencies {
        compile group: "de.jensd", name: "fontawesomefx", version: "8.3"
        compile group: "log4j", name: "log4j", version: "1.2.17"
        compile project(':TweetWallFX-Tweet-API')
        compile project(':TweetWallFX-CommandlineArguments')
    }
}

project(":TweetWallFX-2D"){
    dependencies {
        compile project(':TweetWallFX-Core')
        compile project(':TweetWallFX-Controls')
    }
}

project(":TweetWallFX-3D"){
    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile project(':TweetWallFX-Core')
    }
}

project(":TweetWallFX-Controls"){
    dependencies {
        compile project(':TweetWallFX-Core')
    }
}

project(":TweetWallFX-Devoxx"){
    
    apply plugin: "application"
    mainClassName = "org.tweetwallfx.devoxx.Main"

    tasks.withType(JavaExec) {
        main mainClassName
    }

    task('debug', dependsOn: 'classes', type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        debug = true
    }

    dependencies {
        compile project(':TweetWallFX-2D')
        compile project(':TweetWallFX-Controls')
        runtime project(':TweetWallFX-Tweet-Impl-Twitter4J')
    }
}

project(":TweetWallFX-NetBeans"){
    
    apply plugin: "application"
    mainClassName = "org.tweetwallfx.generic.Main"

    tasks.withType(JavaExec) {
        main mainClassName
        args "-title", "The JavaFX Tweetwall for NetBeans Day!"
        args "-query", "#netbeans OR #netbeansday"
        args "-stylesheet", file('src/main/resources/netbeans.css').toURI().toURL().toString()
    }

    run {
        classpath project(':TweetWallFX-Tweet-Impl-Twitter4J').sourceSets.main.runtimeClasspath
    }    
    task('debug', dependsOn: 'classes', type: JavaExec) {
        classpath sourceSets.main.runtimeClasspath
        classpath project(':TweetWallFX-Tweet-Impl-Twitter4J').sourceSets.main.runtimeClasspath
        debug = true
    }

    dependencies {
        compile project(':TweetWallFX-Generic2D')
    }
    
    task('runWithTwitter4J', dependsOn:'run')
}

project(":TweetWallFX-JavaOne") {
    dependencies {
        compile project(':TweetWallFX-2D')
    }
}

project(":TweetWallFX-Generic2D") {
    dependencies {
        compile project(':TweetWallFX-2D')
        compile project(':TweetWallFX-Controls')
    }
}

description = 'TweetWallFX'

task ('runDevoxx', dependsOn: ':TweetWallFX-Devoxx:run')

task ('runNetBeans', dependsOn: ':TweetWallFX-NetBeans:run')
